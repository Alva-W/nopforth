: 1!+ ( byte a -> a+1 )   tuck  b!  1 + ;
: 1@+ ( a -> a+1 byte )   dup b@  swap 1 + swap ;

( Little endian )
macro : le1@   ` b@ ;
forth : le1@   le1@ ;

: le2@ ( a -> n )   1@+ swap  b@ 8 lshift or ;

: le4@ ( a -> n )
   1@+            swap
   1@+  8 lshift  swap
   1@+ 16 lshift  swap
   b@  24 lshift
   or or or ;

: le8@ ( a -> n )
   1@+ swap
   1@+  8 lshift  swap
   1@+ 16 lshift  swap
   1@+ 24 lshift  swap
   1@+ 32 lshift  swap
   1@+ 40 lshift  swap
   1@+ 48 lshift  swap
   b@  56 lshift
   or or or or or or or ;

macro : le1!   ` b! ;
forth : le1!   le1! ;

: le2! ( n a -> )   over swap  1!+   swap 8 rshift   swap b! ;

: le4! ( n a -> )
   over push  1!+
   r@   8 rshift  swap 1!+
   r@  16 rshift  swap 1!+
   pop 24 rshift  swap b! ;

: le8! ( n a -> )
   over push  1!+
   r@   8 rshift  swap 1!+
   r@  16 rshift  swap 1!+
   r@  24 rshift  swap 1!+
   r@  32 rshift  swap 1!+
   r@  40 rshift  swap 1!+
   r@  48 rshift  swap 1!+
   pop 56 rshift  swap b! ;


( Big endian )
macro : be1@   ` b@ ;
forth : be1@   be1@ ;

: be2@ ( a -> n )   1@+ 8 lshift  swap b@ or ;

: be4@ ( a -> n )
   1@+ 24 lshift  swap
   1@+ 16 lshift  swap
   1@+  8 lshift  swap
    b@  or or or ;

: be8@ ( a -> n )
   1@+ 56 lshift  swap
   1@+ 48 lshift  swap
   1@+ 40 lshift  swap
   1@+ 32 lshift  swap
   1@+ 24 lshift  swap
   1@+ 16 lshift  swap
   1@+  8 lshift  swap
    b@  or or or or or or or ;

macro : be1!   ` b! ;
forth : be1!   be1! ;

: be2! ( n a -> )   over 8 rshift  swap 1!+  b! ;

: be4! ( n a -> )
   over 24 rshift  swap 1!+
   over 16 rshift  swap 1!+
   over  8 rshift  swap 1!+  b! ;

: be8! ( n a -> )
   over 56 rshift  swap 1!+
   over 48 rshift  swap 1!+
   over 40 rshift  swap 1!+
   over 32 rshift  swap 1!+
   over 24 rshift  swap 1!+
   over 16 rshift  swap 1!+
   over  8 rshift  swap 1!+  b! ;

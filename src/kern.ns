forth
: cfa ( a -> a' )   >cfa @ ;

macro
: `   next-word mlatest dfind dup 0 = if abort then  cfa call, ;

: asave   ` a    ` push ;
: arest   ` pop  ` a! ;

: begin ( -> a )        here ;
: while ( a -> a' a )   ` if swap ;
: again ( a -> )        ` branch ;
: repeat ( a a' -> )    ` again ` then ;

: for ( -> a a' )    ` push ` begin ` r@ ` while ;
: next ( a a' -> )   ` rdec ` repeat ` rdrop ;

: 2push   ` push ` push ;
: 2pop    ` pop ` pop ;
: 2dup    ` over ` over ;
: 2drop   ` drop ` drop ;

: not   0 ` lit ` = ;


( macro -> forth )
\ We define forth words using the macros, so they can be used interactively
forth
: dup    dup ;
: drop   drop ;
: swap   swap ;
: over   over ;
: nip    nip ;

: 2dup    2dup ;
: 2drop   2drop ;

: a     a ;
: a!    a! ;
: @     @ ;
: @+    @+ ;
: 1@    1@ ;
: 1@+   1@+ ;
: !     ! ;
: !+    !+ ;
: 1!    1! ;
: 1!+   1!+ ;

: +      + ;
: -      - ;
: *      * ;
: /mod   /mod ;
: /      /mod nip ;
: mod    /mod drop ;

: lshift   lshift ;
: rshift   rshift ;
: and      and ;
: or       or ;
: xor      xor ;
: ~        ~ ;
: negate   negate ;

: not      not ;
: =        = ;
: ~=       ~= ;
: <        < ;
: <=       <= ;
: >        > ;
: >=       >= ;

: tuck ( x y -> y x y )   swap over ;
: min ( a b -> a|b )      2dup < if drop exit then nip ;
: max ( a b -> a|b )      2dup > if drop exit then nip ;


( Dictionary )
forth
: find ( a u -> a'|0 )   latest @ dfind ;

: ' ( -> a )      next-word            find dup 0 = if abort then  cfa ;
: f' ( -> a|0 )   next-word flatest @ dfind dup 0 = if abort then  cfa ;

macro
: [']    '  ` lit ;
: [f']   f' ` lit ;
forth

: allot ( n -> )   here +  h ! ;

: pfa ( 'def -> 'data )   \ return a pointer to a definition's data area
   cfa 5 + ;

: (does>)   \ runtime for does>
   latest @ @ cfa
   pop swap call! drop   \ compile a call to the code following does>
   0 hole ! ;            \ do not transform the previous call into a jump

macro
: does>
   [f'] (does>) call,  \ compile a call to the runtime for does>

   \ at runtime R has the address of the data area for the definition,
   \ compile a pop to retrieve that address
   ` pop ;
forth

: created ( a u -> )
   entry,
   >cfa  here swap !  \ store next data address as the cfa
   here call,         \ compile a dummy call
   does> ;

: create   next-word created ;

: variable   create 0 , ;
: value      create ,  does> @ ;

: ?find ( -> a )   next-word find   dup 0 = if abort then ;

macro
: to ( n -> )   ?find pfa  ` lit ` ! ;
forth
: to ( n -> )   ?find pfa  ! ;

8 value cell
: cells ( -> u )   cell * ;

: marker
   here              \ save current HERE so the marker definition removes itself
   forth create
      ,              \ 0:       dictionary pointer
      flatest @ @ ,  \ cell:    next to last forth definition
      mlatest @ ,    \ 2 cells: last macro definition
   does>
      dup        @ h !
      dup cell + @ flatest !
       2 cells + @ mlatest ! ;


( Memory utilities )
forth
: advance ( a u n -> a+n u-n )   swap over  - push + pop ;

: move ( src dst u -> )
   push push a! pop pop
   begin dup while
     over 1@+ swap 1!
     1 advance
   repeat
   drop drop ;

: +! ( n a -> )   swap over  @ +  swap ! ;
: mem, ( a u -> )   here over allot  swap move ;

: mem= ( a1 a2 u -> f )
   dup 0 = if  drop drop drop  -1 exit  then
   push  over 1@ over 1@ ~= if  drop drop rdrop  0 exit then
   push  1 +  pop pop 1 advance  mem= ;

: head ( a u b -> a u' )    push 2dup pop scan  nip - ;
: tail ( a u b -> a' u' )   scan  dup if 1 advance then ;

: align ( a u -> a' )   1 -  swap over +  swap ~ and ;

0 value record:
: field: ( off size -> off' )
   create  over , +
   does>   @ + ;


( Strings )
forth
32 value bl

: emit ( b -> )   here 1!  here 1 type ;

: cr      10 emit ;
: space   bl emit ;
: spaces ( u -> )   for space next ;

: printable? ( c -> flag )    bl 127 within ;

: char ( -> b )     bl word drop 1@ ;
macro
: [char] ( -> b )   char ` lit ;
forth

record:
   cell            field: str-len
   cell align cell field: str-data
value /str

: str! ( a len dst -> )      tuck  str-len !  str-data ! ;
: str>mem ( str -> a len )   dup str-data @  swap str-len @ ;
: str, ( a len -> )
   here push  /str allot  here push
   tuck mem,  pop swap pop str! ;

macro
: strlit ( a len -> )
   ` branch-unresolved  here push push  \ r: 'str 'resolve
   str,  pop ` then  pop ` lit ;

macro : "            [char] " word  ` strlit ;
forth : " ( -> a )   here  [char] " word str, ;

: print ( str -> )   str>mem type ;
: println            print cr ;

macro : ."   ` "  [f'] print call, ;
forth : ."   [char] " word type ;

forth
: str-len= ( str1 str2 -> ? )   str-len @  swap str-len @ = ;

: str= ( str1 str2 -> ? )
   2dup str-len= not if  drop drop 0 exit  then
   str-data @ swap  str>mem  mem= ;

: str-size ( str -> size )   str-len @ /str + ;

: str-copy ( str dst -> )
   push  str>mem
   r@ /str +  over r@ str!
   pop str-data @  swap move ;

: str-concat ( src dst -> )
   push  str>mem  r@ str>mem +  swap dup push  move
   pop  pop str-len  +! ;


: s>z ( a u -> a' )     here push  dup allot  r@ swap move  0 1,  pop ;
: zlen ( a -> u )       a! 0 begin 1@+ while 1 + repeat ;
: z" ( -> a )           [char] " word s>z ;
: z>mem ( a -> a' u )   dup zlen ;

macro
: memlit ( a len -> )
   dup push  ` branch-unresolved  here push push
   \ r: len addr-of-string-start addr-of-branch-dst
   mem,  pop ` then  pop ` lit  pop ` lit ;

macro : ,"   [char] " word  ` memlit ;
forth : ,"   [char] " word   here over allot  swap move ;


: depth ( -> u )   S0 sp@ - 8 /  2 - ;


( Pictured numeric conversion )
forth
: digit ( n -> n' )   dup 9 >  7 and +  48 + ;

: hold ( count rem b -> b count+1 rem )   swap push  swap 1 + pop ;

: <# ( n -> 0 n )               0 swap ;
: #  ( n -> ... count rem )     base @ /mod swap digit hold ;
: #> ( ... count rem -> a u )   asave  drop  here a!  dup push for 1!+ next  here pop  arest ;
: #s ( n -> ... count rem )     begin # dup while repeat ;

: abs ( n -> |n| )   dup 0 < if negate then ;
: sign ( n -> )   0 < if  [char] - hold  then ;

: (.) ( n -> )   dup push abs <#  #s pop sign #> ;
: . ( n -> )     (.) type space ;

: .S   depth S0 16 - swap for  dup @ . 8 -  next drop  ." <- top " ;

: spfill ( u -> )    16 swap - 3 * spaces  space ;
: .addr ( a -> )     base @ push hex  <# cell for # # next #> type  pop base ! ;
: hb. ( u -> )       base @ push hex  <# bl hold # # #> type  pop base ! ;
: bytes ( a u -> )   swap a! for 1@+ hb. next ;
: c. ( c -> )        dup printable? not if  drop [char] .  then  emit ;
: text ( a u -> )    swap a! for 1@+ c. next ;
: line ( a u -> )    over .addr 2 spaces  2dup bytes dup spfill  text cr ;

: dump ( a u -> )
   begin dup while
      2dup 16 min line
       dup 16 min advance
   repeat
   drop drop ;


( Read-eval loop )
forth
0 value 'rel
: rel   'rel execute ;

0 value 'warm
: warm   'warm dup if execute then  bye ;

: .abort ( str -> )
   dup 0 ~= if println exit then  drop
   inbuf @ inpos @ here str!  here print  [char] ? emit cr ;

: abort ( str -> )         .abort reset-input resetstacks warm ;
: ?abort ( flag str -> )   push  if pop abort then  rdrop ;

: ?underflow
   depth 0 >= if exit then
   " stack underflow!" println  0 abort ;

: evaluate
   next-word dup 0 = if  drop drop exit  then
   eval ?underflow evaluate ;

variable 'refill  \ must be set before use
: refill               'refill @ execute ;
: refilled? ( -> n )   inpos @ inused @ ~= if -1 exit then  refill ;

anon: begin refilled? while evaluate repeat ;  to 'rel


( Files )
forth
: create-file ( a u mode -> fd )       push s>z pop syscreate ;
: open-file ( a u flags mode -> fd )   2push s>z 2pop sysopen ;

: read-byte ( fd -> b|-1 )
   push here 1 pop sysread 1 = if  here 1@ exit  then -1 ;

: read-line ( a u fd -> n )
   push push a! pop pop over  for
     dup read-byte dup 1!+
     dup -1 = if  drop drop pop - exit  then
         10 = if  drop pop - 1 + exit  then
   next
   drop ;

: write-byte ( b fd -> n )
   push  here 1!  here 1 pop syswrite ;

: write-line ( a u fd -> n )
   dup push syswrite  10 pop write-byte
   1 ~= if  drop -1 exit  then 1 + ;

: file-position ( fd -> n' )   push 0 1 pop sysseek ;


( File loading )
: input@ ( -> fd buf tot used pos 'refill )
   infd @ inbuf @ intot @ inused @ inpos @ 'refill @ ;

: input! ( fd buf tot used pos 'refill -> )
   'refill ! inpos ! inused ! intot ! inbuf ! infd ! ;

: save-input
   pop pop  input@ push push push push push push  push push ;

: restore-input
   pop pop  pop pop pop pop pop pop input!  push push ;

: fsize ( fd -> u )   push 0 2 pop sysseek ;

: fmap ( fd -> a u ) \ PROT_READ MAP_PRIVATE
   dup push fsize  0 over 1 2 pop 0 sysmmap  swap ;

: inbuf+                inbuf +! ;
: intot-                negate intot +! ;
: setup                 0 inpos !  inused @ dup inbuf+ intot- ;
: ?skip ( a u -> a' )   0 = if exit then   dup 1@ 10 = if 1 + then ;
: eolscan               inbuf @ intot @ 10 scan  ?skip ;
: frefill ( -> n )      setup  eolscan inbuf @ -  dup inused ! ;

: included ( a u -> err )
   0 0 open-file dup 0 < if exit then
   save-input  dup dup fmap 0 0 ['] frefill input!
   push rel pop
   sysclose drop  inbuf @ intot @ sysmunmap drop
   restore-input  0 ;

: include   10 word included " can't include file" ?abort ;


( Shell utilities )
: #!   ` \ ;
: arg ( u -> a u' )   cells args + @ z>mem ;
: getenv ( name -> value )   str>mem s>z (getenv)  z>mem here str!  here ;

variable arg-offset
: next-arg ( -> a u )
   arg-offset @  dup #args >= if  drop 0 0 exit  then
   arg  1 arg-offset +! ;


marker empty

: term-refill ( -> n )
   ." ok "
   0 inpos !  inbuf @ intot @ infd @ read-line  dup inused !
   dup 0 = if cr then ;

: term-loop   0  here 128 dup allot  0 0 ['] term-refill input!  rel ;

' abort 'abort !

anon:
   #args if
      1 arg-offset +!  0 arg included
      " can't run file" ?abort  exit
   then
   banner ['] term-loop to 'warm  term-loop
; execute
